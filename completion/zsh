#!/usr/bin/env zsh

_manage_fallback () {
    if zstyle -T ":completion:${curcontext}:" use-fallback; then
        _path_files
        ret=$?
    else
        _message 'Unknown sub-command'
    fi
}

_manage () {
    local curcontext="$curcontext" state line cmds ret=1

    _arguments -C \
        '1: :->cmds' \
        '*: :->args' && ret=0

    local _script
    local -a _names
    local completions
    key="$(pwd | md5sum)"
    key="_manage_zsh_${key[0,32]}"

    scripts=("${(@f)$(manage completion)}")

    for _script in $scripts[@]
    do
        _script=("${(s/:/)_script}")
        _names+=("${_script[1]}")
    done

    case "$state" in
        (cmds)
            _describe -t scripts 'scripts' scripts && ret=0
            ;;
        (args)
            if (( ${_names[(I)${line[1]}]} ))
            then
                local prev
                local cmd
                cmd="${line[1]}"
                prev="${words[CURRENT-1]}"

                if  [[ -z "${prev}" ]]
                then
                    completions=("${(@f)$(manage completion "${cmd}")}")
                else
                    completions=("${(@f)$(manage completion "${cmd}" "${prev}")}")
                fi

                if  [ -z "${completions}" ]
                then
                    _manage_fallback
                else
                    _describe -t completions "${cmd}" completions && ret=0
                fi
            else
                _manage_fallback
            fi
            ;;
    esac

    return ret
}
